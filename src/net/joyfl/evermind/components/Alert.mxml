<?xml version="1.0" encoding="utf-8"?>
<s:SkinnablePopUpContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
						   xmlns:s="library://ns.adobe.com/flex/spark"
						   width="350" height="250">
	
	<fx:Declarations>
		<fx:String id="title" />
		<fx:String id="text" />
	</fx:Declarations>
	
	<s:Panel title="{title}" backgroundColor="0xF2F2F2" width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout paddingLeft="30" paddingTop="10" paddingRight="30" paddingBottom="10" />
		</s:layout>
		
		<s:Label text="{text}" width="100%" height="100%" />
		<s:HGroup id="buttonContainer" width="100%" horizontalAlign="center" mxmlContent="{buttons}" />
	</s:Panel>
	
	<fx:Script>
		<![CDATA[
			/**
			 * @author xoul (http://xoul.kr)
			 */
			
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			
			import spark.components.Button;
			
			[Bindable]
			private var buttons : Array = [];
			
			public static function show( parent : DisplayObjectContainer, buttonLabels : Array, text : String = "", title : String = "", closeHandler : Function = null, modal : Boolean = false ) : Alert
			{
				var alert : Alert = new Alert();
				alert.text = text;
				alert.title = title;
				alert.buttonLabels = buttonLabels;
				alert.open( parent, modal );
				PopUpManager.centerPopUp( alert );
				
				if( closeHandler != null )
					alert.addEventListener( CloseEvent.CLOSE, closeHandler );
				
				return alert;
			}
			
			public function set buttonLabels( newButtonLabels : Array ) : void
			{
				buttons.length = 0;
				
				for( var i : int = 0; i < newButtonLabels.length; i++ )
				{
					var button : Button = new Button();
					button.label = newButtonLabels[i];
					button.percentWidth = 100;
					button.addEventListener( MouseEvent.CLICK, onButtonClick );
					buttons.push( button );
				}
			}
			
			private function onButtonClick( e : MouseEvent ) : void
			{
				var closeEvent : CloseEvent = new CloseEvent( CloseEvent.CLOSE );
				closeEvent.detail = buttonContainer.getChildIndex( e.target as Button );
				this.dispatchEvent( closeEvent );
				PopUpManager.removePopUp( this );
			}
		]]>
	</fx:Script>
	
</s:SkinnablePopUpContainer>
